// Notify current state on startup
rule "Startup"
when
    System started
then
    logInfo("RuleEngine", "Rule at system startup")
    val actions = getActions("mqtt","mqtt:broker:pshActor1")
    actions.publishMQTT("/Pi1/In/Admin","NOTIFY_CURRENT_STATE")    
end


// Switch on lights
rule "Start night lights on sunset"
when
    Channel "astro:sun:home:set#event" triggered
then
    switch(receivedEvent) {
        case "START": {
            gNightLights.sendCommand(ON)
        }
    }
end

// Switch on night lights early morning
rule "Switch on night lights early morning"
when
    Time cron "0 30 6 * * ?"
then
    if (Sun_Elevation.state < 0|Â°) {
        logInfo("Astro", "Sun is still down, elevation {}, lights to be enabled", Sun_Elevation.state)
        gNightLights.sendCommand(ON)
    } else {
        logInfo("Astro", "Sun is up, elevation {}, no need to enable lights", Sun_Elevation.state)
        gNightLights.sendCommand(OFF)
    }
end

// Switch on Xmas lights
rule "Switch on Xmas lights"
when
    Time cron "0 0 17 6-31 DEC ? *" or
    Time cron "0 0 17 1-15 JAN *" or
    Time cron "0 0 6 6-31 DEC ? *" or
    Time cron "0 0 6 1-15 JAN *" 
then
    gXmasLights.sendCommand(ON)
    gNightLights.sendCommand(OFF)
end

// Switch off all night lights every day at 0:00
rule "Off night lights"
when
    Time cron "0 0 0 * * ?"
then
    gNightLights.sendCommand(OFF)
    gXmasLights.sendCommand(OFF)
end

// Switch off lights
rule "Switch off night lights on sunrise"
when
    Channel "astro:sun:home:rise#event" triggered
then
    switch(receivedEvent) {
        case "START": {
            logInfo("Astro", "Sun rise occured, elevation {}, lights to be disables", Sun_Elevation.state)
            gNightLights.sendCommand(OFF)
            gXmasLights.sendCommand(OFF)
        }
    }
end
