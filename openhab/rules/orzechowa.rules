// Notify current state on startup
rule "Startup"
when
    System started
then
    logInfo("RuleEngine", "Rule at system startup")
    val actions = getActions("mqtt","mqtt:broker:pshActor1")
    actions.publishMQTT("/Pi1/In/Admin","NOTIFY_CURRENT_STATE")    
end


// Switch on lights
rule "Start night lights on sunset"
when
    Channel "astro:sun:home:set#start" triggered
then
    logInfo("Sunset Rule", "Sunset occurred, scheduling night lights ON in 30 minutes")
    createTimer(now.plusMinutes(30), [ |
            gNightLights.sendCommand(ON)
            if (Away.state == ON) {
            	gAwayLights.sendCommand(ON)
            }
    ])
end

// Switch on Xmas lights
rule "Switch on Xmas lights"
when
    Time cron "0 0 16 6-31 DEC ? *" or
    Time cron "0 15 16 1-15 JAN *"
then
    gNightLights.sendCommand(OFF)
    gXmasLights.sendCommand(ON)
end

// Switch off all night lights every day at 23:00
rule "Off night lights"
when
    Time cron "0 0 23 * * ?"
then
    gNightLights.sendCommand(OFF)
    gXmasLights.sendCommand(OFF)
    gAwayLights.sendCommand(OFF)
end

// Switch off lights
rule "Switch off night lights on sunrise"
when
    Channel "astro:sun:home:rise#event" triggered
then
    switch(receivedEvent) {
        case "START": {
            logInfo("Astro", "Sun rise occured, elevation {}, lights to be disables", Sun_Elevation.state)
            gNightLights.sendCommand(OFF)
            gXmasLights.sendCommand(OFF)
        }
    }
end
